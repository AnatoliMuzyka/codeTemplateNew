<!DOCTYPE html>
<html>
  <head>
    <title>ОСВ программно</title>
  </head>
  <body>
	<h1>ОСВ программно</h1>
    <p>
		<code>
			<pre>
			<a href="https://forum.infostart.ru/forum29/topic189730/">ссылка на статью</a>
			<a href="https://infostart.ru/1c/articles/345249/">полезная статья</a>
			
#Область ОСВ
 &НаКлиенте
Процедура ОтчетВзаиморасчеты()
	ИмяОтчета = "ОборотноСальдоваяВедомостьПоСчету";
	//Заполняем реквизиты отчета и обязательные поля (могут отличаться в зависимости от отчета)
	ПользовательскиеНастройкиКомпоновкиДанных = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("Счет" , ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.Материалы"));
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("НачалоПериода" , НачалоГода(ДатаСоставления));
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("КонецПериода" , КонецГода(ДатаСоставления));
	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("Организация" , Организация);

	//Заполняем дополнительные отборы
	НовыйОтбор = ПользовательскиеНастройкиКомпоновкиДанных.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	НовыйОтбор.ИдентификаторПользовательскойНастройки = "Отбор";

	// зададим Отбор
	ЗаполнитьЭлементОтбораКомпоновкиДанных(НовыйОтбор,Ложь, ВидСравненияКомпоновкиДанных.Равно,"Подразделение", );
	ЗаполнитьЭлементОтбораКомпоновкиДанных(НовыйОтбор,Истина, ВидСравненияКомпоновкиДанных.ВСписке,"Субконто2", СписокСкладов);
	ЗаполнитьЭлементОтбораКомпоновкиДанных(НовыйОтбор,Истина, ВидСравненияКомпоновкиДанных.ВСписке,"Субконто1", СписокНоменклатуры);

	//Заполняем группировки
	Группировка1 = Новый Массив;
	ОписаниеГруппировки = Новый Структура("Использование,Поле,Представление", Истина, "Субконто1", "Контрагенты");
	Группировка1.Добавить(ОписаниеГруппировки);
	ОписаниеГруппировки = Новый Структура("Использование,Поле,Представление", Истина, "Субконто2", "Договоры");
	Группировка1.Добавить(ОписаниеГруппировки);
	ОписаниеГруппировки = Новый Структура("Использование,Поле,Представление", Истина, "Субконто3", "Документы расчетов с контрагентом");
	Группировка1.Добавить(ОписаниеГруппировки);

	ПользовательскиеНастройкиКомпоновкиДанных.ДополнительныеСвойства.Вставить("Группировка", Группировка1);

	//Обязательно указываем ключ (Равным параметру "ИДРасшифровки",я указывал Имя отчета ) НастройкиРасшифровки
	НастройкиРасшифровки = Новый Структура;
	НастройкиРасшифровки.Вставить(ИмяОтчета, ПользовательскиеНастройкиКомпоновкиДанных);

	УсловияОтбора = Новый Структура();
	УсловияОтбора.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	//Дублируем настройки (у меня в файловой без этого не открывалось)
	ОбщиеНастройки = Новый Структура();
	ОбщиеНастройки.Вставить("Отчет", УсловияОтбора);//Объект
	ОбщиеНастройки.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
	//Помещаем во временное хранилище (Уид можно случайный)
	АдресХранилища = ПоместитьВоВременноеХранилище(ОбщиеНастройки, Новый УникальныйИдентификатор);
	//Обращаем внимания на заполнемые настройки, если вы укажете отбор = истина то затрется все выше указанные отборы
	ЗаполнятьТиповыеНастройки = Новый Структура;
	ЗаполнятьТиповыеНастройки.Вставить("Отбор" , Ложь);
	ЗаполнятьТиповыеНастройки.Вставить("Группировка" , Ложь);
	ЗаполнятьТиповыеНастройки.Вставить("ВыводимыеДанные", Истина);
	ЗаполнятьТиповыеНастройки.Вставить("Показатели" , Истина);

	//Сами параметры для предачи на форму ("ИДРасшифровки" - в данном случаи ключ от структуры НастройкиРасшифровки , если "СформироватьПриОткрытии" будет равно Ложь то отчет откроется с ранее сохраненными настройками)
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("ВидРасшифровки" , 1);
	ПараметрыОтчета.Вставить("АдресНастроек" , АдресХранилища);
	ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОтчета.Вставить("ИДРасшифровки" , ИмяОтчета);
	ПараметрыОтчета.Вставить("РежимРасшифровки" , Истина);
	ПараметрыОтчета.Вставить("ЗаполняемыеНастройки" , ЗаполнятьТиповыеНастройки);

	ОткрытьФорму("Отчет." + ИмяОтчета + ".Форма.ФормаОтчета", ПараметрыОтчета,ЭтаФорма,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЭлементОтбораКомпоновкиДанных(ОтборНастройкиКомпоновкиДанных,Использование,ВидСрав,ИмяПоля,ЗначениеОтбора)
    
    НовыйЭлемент = ОтборНастройкиКомпоновкиДанных.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    
    НовыйЭлемент.Использование  = Использование;
    НовыйЭлемент.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ИмяПоля);
    НовыйЭлемент.ВидСравнения   = ВидСрав;
    НовыйЭлемент.ПравоеЗначение = ЗначениеОтбора;
    
КонецПроцедуры
 #КонецОбласти
			</pre>
		</code>
	</p>
 </body>
</html>